terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 6.0"
    }
  }
}

provider "aws" {
  profile = "sherlihydtcom"
}

variable "bucket_id" {
  type = string
}

locals {
  mime_types = {
    "html" = "text/html"
    "css"  = "text/css"
    "js"   = "application/javascript"
    "png"  = "image/png"
    "jpg"  = "image/jpeg"
    "gif"  = "image/gif"
  }
}

resource "terraform_data" "replacement" {
  input = timestamp()
}

resource "aws_s3_object" "website" {
  for_each = fileset("${path.module}/website", "**/*")

  bucket = var.bucket_id
  key    = each.value
  source = "${path.module}/website/${each.value}"
  source_hash = filemd5("${path.module}/website/${each.value}")

  content_type = lookup(local.mime_types, split(".", each.value)[length(split(".", each.value)) - 1], "application/octet-stream")


   lifecycle {
      replace_triggered_by = [terraform_data.replacement]
   }
}
